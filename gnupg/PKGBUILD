# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=gnupg
pkgver=2.2.29
pkgrel=0
pkgdesc='Complete and free implementation of the OpenPGP standard'
provides=('dirmngr' "gnupg2=${pkgver}")
url='https://gnupg.org/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('gettext-devel'
             'libassuan-devel'
             'libbz2-devel'
             'libcurl-devel'
             'libgcrypt-devel'
             'libgnutls-devel'
             'libgpg-error-devel'
             'libiconv-devel'
             'libksba-devel'
             'libnettle-devel'
             'libnpth-devel'
             'libp11-kit-devel'
             'libreadline-devel'
             'libsqlite-devel'
             'libtasn1-devel'
             'libunistring-devel'
             'nettle'
             'tar'
             'zlib-devel')
optdepends=('curl: gpg2keys_curl')
depends=('bzip2'
         'libassuan'
         'libbz2'
         'libcurl'
         'libgcrypt'
         'libgpg-error'
         'libgnutls'
         'libiconv'
         'libintl'
         'libksba'
         'libnpth'
         'libreadline'
         'libsqlite'
         'nettle'
         'pinentry'
         'zlib'
        )
source=("https://gnupg.org/ftp/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2"{,.sig}
        'gnupg-2.2.8-msys2.patch'
        '0001-gnupg-2.2.9-have-drive-letters.patch'
        '0003-scd-Fix-for-SCARD_IO_REQUEST-structure.patch')
sha256sums=('6ff891fc7583a9c3fb9f097ee0d1de0a12469d4b53997e7ba5064950637dfaec'
            'SKIP'
            'cb23f1a61fd213c25e85b6ba8afb190b7da14a8cfa59cc56ce82df941db8c3c9'
            'bf9d6675024fa21003f48fc706a5f80799362b7370db0a6de19be36cf73919a5'
            'fe8c17bf1ab486f275e270048242d891881f733eda399f4a0acea36033f77df3')
validpgpkeys=('031EC2536E580D8EA286A9F22071B08A33BD3F06'
              'D8692123C4065DEA5E0F3AB5249B39D24F25E3B6'
              '6DAA6E64A76D2840571B4902528897B826403ADA')
install=${pkgname}.install

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed '/noinst_SCRIPTS = gpg-zip/c sbin_SCRIPTS += gpg-zip' -i tools/Makefile.in
  patch -p1 -i ${srcdir}/gnupg-2.2.8-msys2.patch
  patch -p1 -i ${srcdir}/0001-gnupg-2.2.9-have-drive-letters.patch
  patch -p1 -i ${srcdir}/0003-scd-Fix-for-SCARD_IO_REQUEST-structure.patch

  autoreconf -fiv

  sed 's/development_version=yes/development_version=no/' -i configure
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/gnupg \
    --disable-libdns \
    --enable-maintainer-mode

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
